title: Inter-Party Relationships
new-page: true
view-if: year <= 1932 and inter_party_relationships_timer <= 0
tags: party_affairs
is-card: true
on-arrival: month_actions += 1; inter_party_relationships_timer = 6;
card-image: img/weimar_coalition_2.jpg
max-visits: 12

= Inter-Party Relationships

The PSOE cannot govern by itself yet. Because we do not have an electoral majority, we need the support of other parties. Recently, our governments have consisted of the Weimar Coalition of the PSOE, Center, and Democratic parties. We can reach out and build additional connections with these parties, or perhaps we can stand alone.

- @liberals: Strengthen our bonds with the left-liberal German Democratic Party and conservative-liberal German People's Party.
- @center: Strengthen our bonds with the Catholic Center Party.
- @pce: Work towards repairing relationships with the Communist Party.
- @psoe_alone: The PSOE can stand alone for now.
- @easy_discard


@pce
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; pce_inter_party_seen += 1
go-to: pce_blutmai if blutmai = 1; pce_1 if blutmai = 0 and pce_coalition_success = 0; pce_2 if pce_coalition_success = 1

@pce_1
on-arrival: pce_relation += 6*(1 - dissent); communist_coalition += 1 if pce_inter_party_seen <= 1; reformist_dissent += 2; erc_relation -= 2; prp_relation -= 3

The communists are suspicious of our overtures, especially the Stalinist leadership, who persist in calling us "social fascists". However, connections are made at the grassroots and local levels.

@pce_2
on-arrival: pce_relation += 8*(1 - dissent); communist_coalition += 1 if pce_inter_party_seen <= 1; reformist_dissent += 2; erc_relation -= 3; prp_relation -= 3

After the success of the coalition government with the PCE, they are much more amenable to us.

@pce_blutmai
on-arrival: pce_relation += 3*(1 - dissent)

After the events of Bl端tmai, reconciling with the Communists is difficult, if not impossible. Nevertheless, we must try.

@center
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; erc_relation += 2 if erc_party_leader == "Joos"
go-to: center_op if erc_in_government = 1 and psoe_in_government = 0 and chancellor != "Br端ning"; center_coalition if psoe_in_government = 1 and erc_in_government = 1; center_bruning if chancellor = "Br端ning"

@center_op
on-arrival: erc_relation += 5*(1 - dissent)

We are currently in opposition to the Center-led government, but that does not preclude the general building of feelings of good will.

@center_coalition
on-arrival: erc_relation += 8*(1 - dissent)

We are currently coalition partners with them, so improving relations comes naturally.

@center_bruning
on-arrival: erc_relation += 5*(1 - dissent)

The Br端ning semi-autocratic regime makes building friendly relations difficult, but not impossible.

@liberals
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; izqd_rep_relation += 5*(1 - dissent)
go-to: prp_stresemann if stresemann_dead = 0; prp_loss if stresemann_dead = 1 and year <= 1930; prp_moribound if year > 1930

The IR has been one of our closest allies through the course of the Republic.

@prp_stresemann
on-arrival: prp_relation += 8*(1 - dissent)

Gustav Stresemann, the leader of the PRP is the best foreign policy leaders of the republic, and while he has previously expressed monarchist tendencies, he has been friendly towards us.

@prp_loss
on-arrival: prp_relation += 5*(1 - dissent)

With the death of Gustav Stresemann, the PRP is much less likely to be accommodating of us. In addition, the liberal parties are electorally in decline, so perhaps our efforts are best spent elsewhere.

However, given the influence of the PRP among the other right-leaning parties [? if president == "Hindenburg": and with President Hindenburg ?], it can still be helpful to influence them.

@prp_moribound
on-arrival: prp_relation += 5*(1 - dissent)

The liberal parties (both the IR and PRP) are electorally moribund, so perhaps our efforts are best spent elsewhere.

However, given the influence of the PRP among the other right-leaning parties[? if president == "Hindenburg": and with President Hindenburg ?], it can still be helpful to influence them.

@psoe_alone

Whether it is because we *cannot* or *will not*, we do not make ties with other parties at this moment.

- @return: Continue...
