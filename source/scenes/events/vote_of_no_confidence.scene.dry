title: The Right calls a vote of no confidence against the current government!
subtitle: We have angered our coalition partners too much...
new-page: true
view-if: psoe_in_government = 1 and ((in_grand_coalition = 1 or in_popular_front = 1 or in_minority_government = 1) and coalition_dissent >= 3) or (in_weimar_coalition = 1 and coalition_dissent >= 4) and psoe_r < 50 and not constructive_vonc
tags: event

# Options: spend relations to reduce coalition_dissent, ask for pce to abstain
# (lmao PCE toleration arc...)

= Vote of No Confidence

[? if in_grand_coalition : The PRP ?][? if in_weimar_coalition or in_popular_front or in_minority_government: The Center Party ?] has called for a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

- @try_assuage: Try to assuage our partners.
- @support_pce: Could we get the PCE to abstain?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage our partners?

- @give_up_prussia: Give up on leadership of Prussia.
- @austerity: Enact a massive austerity plan.
- @resources: Use our resources to reduce dissent within the coalition.
- @support_pce: Could we get the PCE to abstain?
- @let_it_happen: Never mind. Let the vote happen.

@give_up_prussia
view-if: psoe_prussia == 1
on-arrival: coalition_dissent = 0; psoe_prussia = 0; prussian_government = "I"; reformist_dissent += 10; centrist_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_psoe -= 5; rural_psoe -= 5; 

Fine. We no longer control the government of Prussia. The vote has been called off.

@austerity
view-if: welfare >= 0
on-arrival: coalition_dissent = 0; workers_psoe -= 10; umemployed_psoe -= 10; workers_pce += 7; workers_fe_jons += 3; unemployed_pce += 5; unemployed_fe_jons += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; pce_coalition_dissent += 1; pce_relation -= 10 if in_popular_front

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@resources
choose-if: resources >= 3
unavailable-subtitle: We do not have enough resources.
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@support_pce
choose-if: (in_grand_coalition and communist_coalition >= 3 and pce_relation >= 50 and pce_r >= prp_r) or (in_weimar_coalition and pce_relation >= 50 and pce_r >= erc_r and communist_coalition >= 3)
unavailable-subtitle: [? if communist_coalition < 3 : The PCE won't take part in parliamentary government. ?] [? if pce_relation < 50 : The PCE is not friendly enough with us. ?] [? if (in_grand_coalition and pce_r < prp_r) or (in_weimar_coalition and pce_r < erc_r): The PCE does not have enough votes. ?] [? if in_popular_front : We are already in a Popular Front with the PCE. ?]

If the Communists abstain, then we essentially can maintain a minority government[? if president=="Alcala Zamora":, although it would be very difficult to make policy without the support of President Alcala Zamora, which we do not have ?]. If this happens, then we will own a massive debt to the PCE. This could lead to broader cooperation in the future, for better or for worse.

- @support_pce_2: Persuade the PCE to "tolerate" us?
- @let_it_happen: Never mind. Let the vote happen.

@support_pce_2
on-arrival: communist_coalition += 1; workers_pce += 3; pce_relation += 5
go-to: support_pce_weimar if in_weimar_coalition; support_pce_grand if in_grand_coalition

The PCE is not interested in a "toleration" arrangement; they will only support us if they can participate in the government as part of a coalition. And, if this toleration agreement happens, the right-wing parties will not be inclined to support us. 

@support_pce_grand

- @election_1928.popular_front_coalition: Form a "Popular Front" coalition?
- @election_1928.left_coalition_possible: Form a "United Front".
- @try_assuage: Never mind. We must deal with this some other way.

@support_pce_weimar

- @election_1928.left_coalition_possible:Form a "United Front".
- @try_assuage: Never mind. We must deal with this some other way.


@let_it_happen
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; psoe_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; psoe_toleration = 0; foreign_minister_party = "PRP"; interior_minister_party = "ERC"; justice_minister_party = "I"; labor_minister_party = "ERC"; reichswehr_minister_party = "I"; economic_minister_party = "IR"; finance_minister_party = "PRP"; agriculture_minister_party = "ERC"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann"; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener";  coalition_dissent = 0; pro_republic -= 5; landholders_psoe -= 3; new_middle_psoe -= 2; workers_psoe -= 3 if pro_republic < 50; workers_psoe -= 4 if pro_republic < 40; landholders_fe_jons += 4 if pro_republic < 50; new_middle_fe_jons += 3 if pro_republic < 50
go-to: let_it_happen_alcala_zamora if president != "Aza単a"; let_it_happen_azana if president == "Aza単a"

@let_it_happen_alcala_zamora
on-arrival: chancellor = "Br端ning"; chancellor_party = "ERC"; welfare -= 2;

The vote succeeds, and our government has resigned. Chancellor Br端ning has been appointed as the head of an acting government, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_azana
on-arrival: psoe_caretaker = 1

The vote succeeds, and our government has resigned. Chancellor [+ chancellor +] remains as the head of a caretaker government, but no longer has the power to enact new policies.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
