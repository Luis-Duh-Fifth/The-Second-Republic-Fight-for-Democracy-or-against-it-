title: Presidential Election
subtitle: Hindenburg runs for re-election...
view-if: year = 1932 and (month = 4 or month = 5) and presidential_election_seen = 0
frequency: 1000
max-visits: 1
new-page: true
on-arrival: braun_campaign = 0; hitler_support_hindenburg = 0; presidential_election_seen = 1; fe_jons_candidate = "Hitler" if hitler_deported == 0 else fe_jons_candidate = "Göring";
tags: event
go-to: election_algorithm
set-jump: pres_election

@pres_election

The presidential election is coming up soon. [? if psoe_in_government = 0 : While the position of Chancellor is typically more important, recent governments have depended on the president's emergency decree powers, making the office vital. ?] [? if hitler_deported==0 : The Nazis will run Hitler, while the PCE will run Thälmann. A victory for Hitler would mean the end of parliamentary democracy, and the beginning of an era of unspeakable horrors. ?][? if hitler_deported==1 : Since Hitler has been deported, the Nazis will run Hermann Göring in his stead. He is widely seen as a proxy for Hitler, who, despite his legal persecution, retains a popular base who wishes to see him returned to Germany. Meanwhile, the PCE will run Ernst Thälmann. ?]

# if hitler is deported, who would fe_jons run? Duesterberg? or Goering?
# Goering runs, essentially as a proxy for Hitler, who is now stateless.

Many are calling for us to support the incumbent Hindenburg, despite his well-known reactionary tendencies. Is he truly the best bulwark against Hitler's ascension? On the other hand, many in our party are calling for Otto Braun to run again, but he is reluctant.

[? if pce_relation >= 50 : Another strange possibilty is an united front with the Communists, either to support Thälmann or our own candidate. ?]

- @support_hindenburg: Support Hindenburg.
- @support_braun: Run Otto Braun of the PSOE.
- @support_thalmann: Support Ernst Thälmann of the PCE.

@support_hindenburg
subtitle: This is the default option.

Despite his known reactionary nature, Hindenburg has at least been keeping the Nazis at bay. Thus, we must support him.

- @final_election: The final vote...

@support_braun
subtitle: This will cost us 2 resources.
choose-if: resources >= 2
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 2; braun_campaign = 1
go-to: braun_campaign

It takes effort to run a somewhat unexpected campaign. First, Braun has to be convinced to run. He himself had supported voting for Hindenburg, and it takes a good deal of prodding to convince him of his destiny.

The miniscule remnants of the Democratic Party (now called the German State Party) will likely support us. Who else will? Can we convince the Center to support Braun? What about the Communists?


@support_thalmann
choose-if: pce_relation >= 50 and reformist_strength < left_strength
unavailable-subtitle: [? if pce_relation < 50 : We do not have good enough relations with the Communists, so they are not even willing to accept our support. ?] [? if reformist_strength >= left_strength : The reformist faction is able to dissuade the party from supporting this arrangement. ?]

Supporting Thälmann is rather unpopular in our party, even if the Communists accept our support. Are we sure?

- @support_thalmann_2: Yes, we will support Thälmann, despite the reformists' pleas against it.
- @support_braun: Support Braun instead.
- @support_hindenburg: Support Hindenburg instead.

@support_thalmann_2
on-arrival: psoe_support_thalmann = 1; reformist_dissent += 15; center_dissent += 10; labor_dissent += 5; neorevisionist_dissent += 10; catholics_psoe -= 5; workers_psoe -= 3; pce_relation += 10; communist_coalition += 2; erc_relation -= 15; prp_relation -= 15

Fine. We have thrown our support to Chairman Thälmann of the PCE.

- @final_election: The final vote...

@braun_campaign

- @convince_erc: Convince the Center Party.
- @convince_pce: Convince the Communists.
- @convince_liberals: Convince the remaining liberals, wherever they are.
- @final_election: The final vote...

@convince_erc
choose-if: psoe_votes >= erc_votes
unavailable-subtitle: The Center knows that they are more popular than us; they do not see a reason to support our candidate.
on-arrival: {!
Q.erc_support_braun = 0;
if (Q.erc_relation >= 55) {
    Q.erc_support_braun = 1;
} else if (Q.erc_relation <= 35) {
    Q.erc_support_braun = 0;
} else {
    Q.erc_support_braun = 2;
}
!}
go-to: erc_failed if erc_support_braun = 0 or (erc_support_braun == 2 and historical_mode); erc_success if erc_support_braun = 1; erc_partial if erc_support_braun = 2 and not historical_mode
max-visits: 1

We remind the Center leaders of Hindenburg's anti-Catholicism, and try to convince them that Hindenburg's path will eventually lead to Hitler. We also remind them of the successes they've had as part of the Weimar coalition in Prussia with Otto Braun, and our support for the Center candidate Wilhelm Marx in the 1925 presidential election.

@erc_success
go-to: braun_campaign

The Center are convinced! They will be part of the Braun coalition.

@erc_partial

The Center are not satisfied with our arguments, but they are open to being convinced. They are willing to run a joint campaign if we front the resources.

- @pay_erc: Give them the resources they need.
- @braun_campaign: Forget about the Center.

@pay_erc
choose-if: resources >= 3
subtitle: -3 resources
unavailable-subtitle: Not enough resources (need 3)
on-arrival: resources -= 3; erc_support_braun = 1
go-to: braun_campaign

With our resources, the Center are convinced to support Braun over Hindenburg.

@erc_failed
go-to: braun_campaign

Unfortunately, we fail to turn them away from Hindenburg.

@convince_pce
choose-if: psoe_votes >= pce_votes
unavailable-subtitle: The PCE knows that they are more popular than us; they do not see a reason to support our candidate.
max-visits: 1

The PCE is supporting their own candidate, Ernst Thälmann. [? if pce_relation >= 50 : However, they are willing to be convinced to drop their campaign and support Braun; how do we convince them? ?][? if pce_relation < 50 : They do not seem willing to be convinced. ?]

- @pce_resources: Use our resources.
- @pce_relations: Use our good relations.
- @braun_campaign: Forget about the PCE.

@pce_resources
view-if: pce_relation >= 50 and not historical_mode
choose-if: resources >= 3
subtitle: -3 resources
unavailable-subtitle: Not enough resources (need 3).
on-arrival: resources -= 3; pce_support_braun = 1
go-to: braun_campaign

The PCE now endorses Braun in the presidential election! We ask them to keep their support low-key, so as to not alienate the bourgeois voters.

@pce_relations
view-if: pce_relation >= 60
unavailable-subtitle: Our relations are not good enough.
on-arrival: pce_support_braun = 1
go-to: braun_campaign

The PCE now endorses Braun in the presidential election! We ask them to keep their support low-key, so as to not alienate the bourgeois voters.



@convince_liberals
max-visits: 1
go-to: convince_liberals_success if prp_relation >= 50; convince_liberals_fail if prp_relation < 50

The liberal vote (IR and PRP) has been falling over time, so they will probably be of minimal help to us. For what it's worth, the IR is already willing to support Braun.

@convince_liberals_success
on-arrival: prp_support_braun = 1
go-to: braun_campaign

Thanks to our good relations, the PRP has promised to support Braun in the election.

@convince_liberals_fail
on-arrival: prp_support_braun = 0
go-to: braun_campaign

We failed to convince the PRP to support Braun.



@final_election
on-arrival: election_round = 0
go-to: election_algorithm
set-jump: post_election


@post_election
on-arrival: {!
Q.braun_votes = 0;
Q.hindenburg_votes = 0;
Q.hitler_votes = 0;
Q.thalmann_votes = 0;

// initial phase:
Q.hitler_votes += Q.fe_jons_votes;
Q.hindenburg_votes += Q.other_votes;
Q.hindenburg_votes += Q.ceda_votes;

if (Q.braun_campaign) {
    Q.braun_votes += Q.psoe_votes;
    Q.braun_votes += Q.izqd_rep_votes;
    if (Q.erc_support_braun && Q.erc_support_braun == 1) {
        Q.braun_votes += Q.erc_votes;
    } else {
        Q.hindenburg_votes += Q.erc_votes;
    }
    if (Q.pce_support_braun) {
        Q.braun_votes += Q.pce_votes;
    } else {
        Q.thalmann_votes += Q.pce_votes;
    }
    if (Q.prp_support_braun) {
        Q.braun_votes += Q.prp_votes;
    } else {
        Q.hindenburg_votes += Q.prp_votes;
    }
} else if (Q.psoe_support_thalmann) {
    Q.thalmann_votes += Q.pce_votes;
    Q.thalmann_votes += Q.psoe_votes;
    Q.hindenburg_votes += Q.erc_votes;
    Q.hindenburg_votes += Q.izqd_rep_votes;
    Q.hindenburg_votes += Q.prp_votes;
} else {
    Q.hindenburg_votes += Q.psoe_votes;
    Q.hindenburg_votes += Q.erc_votes;
    Q.hindenburg_votes += Q.izqd_rep_votes;
    Q.hindenburg_votes += Q.prp_votes;
    Q.thalmann_votes += Q.pce_votes;
}
if (Q.hitler_support_hindenburg) {
    Q.hitler_votes -= Q.fe_jons_votes;
    Q.hindenburg_votes += Q.fe_jons_votes;
}

if (Q.hindenburg_to_braun_bonus) {
    Q.hindenburg_votes -= Q.hindenburg_to_braun_bonus;
    Q.braun_votes += Q.hindenburg_to_braun_bonus;
}
if (Q.thalmann_to_braun_bonus) {
    Q.thalmann_votes -= Q.thalmann_to_braun_bonus;
    Q.braun_votes += Q.thalmann_to_braun_bonus;
}

// check for majority
Q.hindenburg_majority = (Q.hindenburg_votes >= 50);
Q.thalmann_majority = (Q.thalmann_votes >= 50);
Q.hitler_majority = (Q.hitler_votes >= 50);
Q.braun_majority = (Q.braun_votes >= 50);
Q.any_majority = Q.hindenburg_majority | Q.thalmann_majority | Q.hitler_majority | Q.braun_majority;

// check for plurality
Q.hindenburg_plurality = (Q.hindenburg_votes >= Q.hitler_votes && Q.hindenburg_votes >= Q.braun_votes && Q.hindenburg_votes >= Q.thalmann_votes);
Q.thalmann_plurality = (Q.thalmann_votes >= Q.hindenburg_votes && Q.thalmann_votes >= Q.braun_votes && Q.thalmann_votes >= Q.hitler_votes);
Q.hitler_plurality = (Q.hitler_votes >= Q.hindenburg_votes && Q.hitler_votes >= Q.braun_votes && Q.hitler_votes >= Q.thalmann_votes);
Q.braun_plurality = (Q.braun_votes >= Q.hindenburg_votes && Q.braun_votes >= Q.hitler_votes && Q.braun_votes >= Q.thalmann_votes);
// If no candidate gets above 50%, then there's a second round. In the second round, a plurality wins. 
Q.election_round += 1;
!}
go-to: hindenburg_wins if hindenburg_majority == 1; thalmann_wins if thalmann_majority == 1; hitler_wins if hitler_majority == 1; braun_wins if braun_majority == 1; no_majority_round1 if any_majority == 0 and election_round == 1; round2_results if election_round >= 2

Results:

Hindenburg - [+ hindenburg_votes +]%

[+ fe_jons_candidate +] - [+ hitler_votes +]%

[? if pce_support_braun == 0 : Thälmann - [+ thalmann_votes +]% ?]

[? if braun_campaign > 0 : Braun - [+ braun_votes +]% ?]

@no_majority_round1
on-arrival: hitler_support_hindenburg = 1 if (braun_plurality or thalmann_plurality)
go-to: round2_menu

There is no majority in this round. In the second round, any candidate who wins a plurality will win overall. What should we do?

[? if braun_plurality or thalmann_plurality : In order to consolidate the vote for the Right, and to prevent a socialist from becoming president, [+ fe_jons_candidate +] is no longer running in the second round, and has switched his endorsement to Hindenburg. ?]


@round2_menu

- @round2_convince_erc: Convince the Center to switch their support to us.
- @round2_convince_pce: Convince the PCE to switch their support to us. 
- @round2_hindenburg: Switch our support to Hindenburg.
- @round2_thalmann: Switch our support to Thälmann of the PCE.
- @round2_vote: Let the second round voting begin.


@round2_convince_erc
view-if: braun_campaign > 0 and erc_support_braun != 1 
choose-if: psoe_votes >= erc_votes
unavailable-subtitle: The Center knows that they are more popular than us; they do not see a reason to support our candidate.
on-arrival: {!
Q.erc_support_braun = 0;

if (Q.erc_relation >= 55) {
    Q.erc_support_braun = 1;
    if (Q.hitler_support_hindenburg) {
        Q.hindenburg_to_braun_bonus += Math.round(Q.other_votes/2);
    }
} else if (Q.erc_relation >= 50 && Q.hitler_support_hindenburg) {
    Q.erc_support_braun = 1;
    Q.hindenburg_to_braun_bonus += Math.round(Q.other_votes/2);
} else if (Q.erc_relation <= 35) {
    Q.erc_support_braun = 0;
} else {
    Q.erc_support_braun = 2;
}
!}
go-to: r2_erc_failed if erc_support_braun = 0 or (erc_support_braun = 2 and historical_mode); r2_erc_success if erc_support_braun = 1; r2_erc_partial if erc_support_braun = 2 and not historical_mode
max-visits: 1

@r2_erc_success
go-to: round2_menu

[? if hitler_support_hindenburg : The Center's opinion is affected by the Nazis' support of Hindenburg, and is willing to turn away from him. ?] The Center are convinced! They will be part of the Braun coalition. [? if hitler_support_hindenburg : In addition, they will also bring in some minor parties who will support us. ?]

@r2_erc_partial

The Center are not satisfied with our arguments, but they are open to being convinced[? if hitler_support_hindenburg: , especially given the Nazis' support for Hindenburg. ?]. They are willing to run a joint campaign if we front the resources.

- @r2_pay_erc: Give them the resources they need.
- @round2_menu: Forget about the Center.

@r2_pay_erc
choose-if: resources >= 2
subtitle: -2 resources
unavailable-subtitle: Not enough resources (need 2)
on-arrival: resources -= 2; erc_support_braun = 1
go-to: round2_menu

With our resources, the Center are convinced to support Braun over Hindenburg.

@r2_erc_failed
go-to: round2_menu

Unfortunately, we fail to turn them away from Hindenburg.




@round2_convince_pce
choose-if: psoe_votes >= pce_votes
unavailable-subtitle: The PCE knows that they are more popular than us; they do not see a reason to support our candidate.
view-if: braun_campaign > 0 and pce_support_braun == 0 
max-visits: 1

The PCE is committed to supporting Ernst Thälmann. [? if pce_relation >= 45 : However, the Nazi-Hindenburg coalition worries them enough that they are willing to be convinced to drop their campaign and support Braun. ?][? if pce_relation < 45 : They do not seem willing to be convinced. ?]

- @r2_pce_resources: Use our resources.
- @r2_pce_relations: Use our good relations.
- @round2_menu: Forget about the PCE.

@r2_pce_resources
view-if: pce_relation >= 45 and not historical_mode
choose-if: resources >= 3
subtitle: -3 resources
unavailable-subtitle: Not enough resources (need 3).
on-arrival: resources -= 3; pce_support_braun = 1
go-to: round2_menu

The PCE now endorses Braun in the presidential election! We ask them to keep their support low-key, so as to not alienate the bourgeois voters.

@r2_pce_relations
view-if: pce_relation >= 60
unavailable-subtitle: Our relations are not good enough.
on-arrival: pce_support_braun = 1
go-to: round2_menu

The PCE now endorses Braun in the presidential election! We ask them to keep their support low-key, so as to not alienate the bourgeois voters. 

@round2_thalmann
view-if: psoe_support_thalmann == 0 and pce_support_braun == 0
choose-if: pce_relation >= 50 and left_strength > reformist_strength 
unavailable-subtitle: [? if pce_relation < 50 : We do not have good enough relations with the Communists, so they are not even willing to accept our support. ?] [? if reformist_strength >= left_strength : The reformist faction is able to dissuade the party from supporting this bizarre arrangement. ?]
max-visits: 1

Supporting Thälmann is rather unpopular in our party, even if the Communists accept our support. Are we sure?

- @round2_thalmann_2: Yes, we will support Thälmann, despite the reformists' pleas against it.
- @round2_menu: Never mind.

@round2_thalmann_2
on-arrival: braun_campaign = 0; psoe_support_thalmann = 1; reformist_dissent += 15; center_dissent += 10; labor_dissent += 5; neorevisionist_dissent += 10; catholics_psoe -= 5; workers_psoe -= 3; pce_relation += 10; communist_coalition += 2; erc_relation -= 15; prp_relation -= 15
on-departure: erc_support_braun = 0; pce_support_braun = 0;

Fine. We have thrown our support to Chairman Thälmann of the PCE.

[? if erc_support_braun : The Center Party is confused at our capricious behavior, and have switched their support to Hindenburg. ?]

- @round2_vote: Let the second round voting begin.

@round2_hindenburg
view-if: braun_campaign > 0 or psoe_support_thalmann > 0
on-arrival: braun_campaign = 0; psoe_support_thalmann = 0

We have switched our support to Hindenburg.

- @round2_vote: Let the second round voting begin.

@round2_vote
go-to: election_algorithm
set-jump: post_election

@round2_results
go-to: hindenburg_wins if hindenburg_plurality == 1; thalmann_wins if thalmann_plurality == 1; braun_wins if braun_plurality == 1; hitler_wins if hitler_plurality == 1


@hindenburg_wins
on-arrival: president = "Hindenburg"

As expected, the incumbent Hindenburg wins the presidency. Let's hope he continues to keep the Nazis out of power.

@thalmann_wins
on-arrival: president = "Thälmann"; pce_relation = 80;

Ernst Thälmann of the PCE has won the presidency. Immediately, the far-right launches a coup attempt against the government. We have no choice but to unite with the PCE against the fascists...

- @civil_war: This is civil war.

@hitler_wins
on-arrival: president = fe_jons_candidate; chancellor = "Hitler"; chancellor_party = "FE_JONS"

[? if fe_jons_candidate == "Hitler" : Adolf Hitler of the FE_JONS has won the presidency. He wastes no time in consolidating power. ?]
[? if fe_jons_candidate != "Hitler" : The FE_JONS candidate [+ president +] has won. The first action President [+ president +] takes is inviting Hitler back into Germany, and appointing him chancellor. The Nazis waste no time in consolidating power. ?]

- @hitler_takes_power: What do we do now?


@braun_wins
on-arrival: president = "Braun"; reformist_dissent -= 15; center_dissent -= 15; labor_dissent -= 15; left_dissent -= 5; workers_psoe += 8*(1-dissent); landholders_psoe += 5*(1-dissent); new_middle_psoe += 5*(1-dissent); rural_psoe += 5*(1-dissent); unemployed_psoe += 5*(1-dissent);

Otto Braun of the PSOE has won the presidency! This is a moment of celebration for our party, and his victory is able to bolster our support.
